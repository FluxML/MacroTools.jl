export @esc, isexpr, isline, rmlines, unblock, namify, isdef, longdef, shortdef, @expand, @hook, makeif

assoc!(d, k, v) = (d[k] = v; d)

macro esc(xs...)
  :($([:($x = esc($x)) for x in esc(xs)]...);)
end

"""
    isexpr(x, ts...)

Convenient way to test the type of a Julia expression.
Expression heads and types are supported, so for example
you can call

    isexpr(expr, String, :string)

to pick up on all string-like expressions.
"""
isexpr(x::Expr) = true
isexpr(x) = false
isexpr(x::Expr, ts...) = x.head in ts
isexpr(x, ts...) = any(T->isa(T, Type) && isa(x, T), ts)

isline(ex) = isexpr(ex, :line) || isa(ex, LineNumberNode)

"""
    rmlines(x)

Remove the line nodes from a block or array of expressions.
"""
rmlines(xs) = filter(x->!isline(x), xs)
rmlines(x::Expr) = Expr(x.head, rmlines(x.args)...)

"""
    unblock(expr)

Remove outer `begin` blocks from an expression, if the block is
redundant (i.e. contains only a single expression).
"""
function unblock(ex)
  isexpr(ex, :block) || return ex
  exs = rmlines(ex).args
  length(exs) == 1 || return ex
  return unblock(exs[1])
end

"""
An easy way to get pull the (function/type) name out of
expressions like `foo{T}` or `Bar{T} <: Vector{T}`.
"""
namify(s::Symbol) = s
namify(ex::Expr) = namify(ex.args[1])

Base.macroexpand(m::Module, ex) =
  eval(m, :(macroexpand($(Expr(:quote, ex)))))

walk(x, inner, outer) = outer(x)
walk(x::Expr, inner, outer) = outer(Expr(x.head, map(inner, x.args)...))

postwalk(f, x) = walk(x, x -> postwalk(f, x), f)
prewalk(f, x)  = walk(f(x), x -> prewalk(f, x), identity)

subs(ex, s, s′) = prewalk(x -> x == s ? s′ : x, ex)

"""
More convenient macro expansion, e.g.

    @expand @time foo()
"""
macro expand(ex)
  :(macroexpand($(Expr(:quote, ex))))
end

"Test for function definition expressions."
isdef(ex) = ismatch(or_(:(function _(__) _ end),
                        :(f_(__) = _)),
                    ex)

function longdef(ex)
  prewalk(ex) do ex
    @match ex begin
      (f_(args__) = body_) => :(function $f($(args...)) $body end)
      _ => ex
    end
  end
end

function shortdef(ex)
  prewalk(ex) do ex
    @match ex begin
      function f_(args__) body_ end => :($f($(args...)) = $body)
      _ => ex
    end
  end
end

function makeif(clauses, els = nothing)
  foldr((c, ex)->:($(c[1]) ? $(c[2]) : $ex), els, clauses)
end

macro hook(ex)
  @capture(shortdef(ex), f_(args__) = body_) || error("Invalid hook $ex")
  sig = map(args) do arg
    @match arg begin
      _::T_... => :(Vararg{$(esc(T))})
      _... => :(Vararg{Any})
      _::T_ => esc(T)
      _     => :Any
    end
  end
  sig = :($(sig...),)
  quote
    let $(esc(:super)) = which($(esc(f)), $sig).func
      $(esc(:(function $f($(args...))
        $body
      end)))
    end
  end
end
